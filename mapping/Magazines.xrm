output r2rml

map Magazine from wikibus.magazines {
	subject template magazineId with Slug;

	graphs
	  template magazineId with Slug;

	types
		wba.Source
		schema.Periodical

	properties
		dcterms.title from Name;
		dcterms.description from SubName;
		wba.issues template magazineIssues with Slug;
}

map MagazineIssue from wikibus.magazineIssues {
	subject template magazineIssueId with MagazineSlug Id;

	graphs
	  template magazineIssueId with MagazineSlug Id;

	types
		wba.Source

	properties
		bibo.pages from Pages;
		dcterms.date from YearMonth with datatype xsd.gYearMonth;
		dcterms.date from Date with datatype xsd.date;
		schema.containedInPlace template storageLocationId with FileCabinet FileOffset;
		schema.isPartOf template magazineId with MagazineSlug;
		schema.contributor template userId with User;
		bibo.issue from MagIssueNumber;
		dcterms.created from DateCreated;
		dcterms.modified from DateUpdated;
		foaf.depiction template magazineIssueImageId with MagazineSlug LegacyImage;
}
