output r2rml

map Magazine from wikibus.magazines {
	subject template magazineId with Slug;

	graphs
	  template magazineId with Slug;

	properties
	    rdf.^type template "api/Periodical" ;
		dcterms.title from Name;
		dcterms.description from SubName;
		wbo.issues template magazineIssues with Slug;
}

map MagazinIssueCollections from wikibus.magazines {
	subject template magazineIssues with Slug;

	graphs
		template magazineIssues with Slug;

	properties
	    rdf.^type template "api/IssueCollection" ;
		hydra.memberAssertion template "issue-collection-{0}-member-assersions" with Slug as BlankNode ;
}

map MagazinIssueCollectionMembersAssertions from wikibus.magazines {
	subject template "issue-collection-{0}-member-assersions" with Slug as BlankNode ;

	graphs
		template magazineIssues with Slug;

	properties
		hydra.property constant schema.isPartOf ;
		hydra.object template magazineId with Slug;
}

map MagazineIssue from wikibus.magazineIssues {
	subject template magazineIssueId with MagazineSlug Id;

	graphs
	  template magazineIssueId with MagazineSlug Id;

	types
		bibo.Issue

	properties
		bibo.pages from Pages;
		dcterms.date from YearMonth with datatype xsd.gYearMonth;
		dcterms.date from Date with datatype xsd.date;
		schema.containedInPlace template storageLocationId with FileCabinet FileOffset;
		schema.isPartOf template magazineId with MagazineSlug;
		bibo.issue from MagIssueNumber;
		schema.image template magazineIssueImageId with MagazineSlug LegacyImage;
}

map MagazineIssueGenerated from wikibus.magazineIssues {
	subject template magazineIssueId with MagazineSlug Id;

	graphs
	  template magazineIssueGenerated with MagazineSlug Id;

	properties
		dcterms.created from DateCreated;
		dcterms.modified from DateUpdated;
}
