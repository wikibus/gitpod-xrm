output r2rml

template brochureImageId "brochure/{0}/image"

map Brochure from wikibus.brochures {
	subject template brochureId with Id;

	graphs
		template brochureId with Id;

	types
		wbo.Brochure

	properties
		bibo.pages from Pages;
		opus.year from Year with datatype xsd.gYear;
		opus.month from Month with datatype xsd.gMonth;
		dcterms.date template date with Year Month Day;
		schema.containedInPlace template storageLocationId with FileCabinet FileOffset;
		dcterms.language template languageId with Language;
		dcterms.title from FolderName;
		dcterms.description from Notes;
		dcterms.identifier from FolderCode;
		schema.contributor template userId with User;
		schema.associatedMedia template brochurePdfId with Id;
		schema.image template brochureImageId with LegacyImage;
		foaf.depiction template imageId with SourceType LegacyImage;
		// schema.primaryImageOfPage template brochureImageId with LegacyImage;
		dcterms.created from DateCreated;
		dcterms.modified from DateUpdated;
		wba.wishlistItem template "wishlist/{0}" with Id;
		schema.identifier from Id;
}

map BrochureFile from wikibus.brochures_files {
	subject template brochurePdfId with Id;
	
	graphs
	 template brochurePdfId with Id;

	types
		schema.MediaObject

	properties
		schema.contentSize from ContentSize;
		dcat.byteSize from ByteSize with datatype xsd.long;
		schema.encodingFormat constant "application/pdf";
		schema.contentUrl from ContentUrl;
		schema.encodesCreativeWork template brochureId with Id;
}
