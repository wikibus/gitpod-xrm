output r2rml

map Brochure from wikibus.brochures {
	subject template brochureId with Id;

	graphs
		template brochureId with Id;

	properties
	    rdf.^type template "api/Brochure" ;
		bibo.pages from Pages;
		dcterms.date from Year with datatype xsd.gYear;
		dcterms.date from YearMonth with datatype xsd.gYearMonth;
		dcterms.date from Date with datatype xsd.date;
		schema.containedInPlace template storageLocationId with FileCabinet FileOffset;
		dcterms.language template languageId with Language;
		dcterms.title from FolderName;
		dcterms.description from Notes;
		dcterms.identifier from FolderCode;
		schema.contributor template userId with User;
		schema.associatedMedia template brochurePdfId with AssociatedMedia;
		schema.image template imageId with SourceType LegacyImage;
		schema.image template imageId with SourceType Image;
		schema.identifier from Id;
}

map BrochureGenerated from wikibus.brochures {
	subject template brochureId with Id;

	graphs
		template brochureGenerated with Id;

	properties
		dcterms.created from DateCreated;
		dcterms.modified from DateUpdated;
		wbo.migrated constant "true";
}

map BrochureFile from wikibus.brochures_files {
	subject template brochurePdfId with Id;
	
	graphs
	 template brochurePdfId with Id;

	types
		schema.MediaObject

	properties
		schema.contentSize from ContentSize;
		dcat.byteSize from ByteSize with datatype xsd.long;
		schema.encodingFormat constant "application/pdf";
		schema.contentUrl template "https://wikibus.blob.core.windows.net/sources{0}/{1}" with Id ContentName;
		schema.encodesCreativeWork template brochureId with Id;
}
